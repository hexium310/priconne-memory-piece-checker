#!/usr/bin/env zsh

red=$(tput setaf 1)
green=$(tput setaf 2)
yellow=$(tput setaf 6)
bold=$(tput bold)
reset=$(tput sgr0)

read-operation() {
    local message="$green${bold}Select operation$reset"

    echo $message >&2

    local input
    input=$({
        echo "${yellow}0$reset Exit"
        echo "${yellow}1$reset Add character"
        echo "${yellow}2$reset Edit character unique equipment"
        echo "${yellow}3$reset Edit character piece type"
        echo "${yellow}4$reset Edit character max rarity"
    } | fzf --ansi --reverse)
    [[ $? = 130 ]] && input='0 Exit'

    tput cuu 1 >&2
    echo "$message $input" >&2

    awk '{ print $1 }' <<< $input
}

add-character() {
    local name initial_rarity max_rarity has_unique_equipment piece_type
    local confirm

    input-data name initial_rarity max_rarity has_unique_equipment piece_type

    local data=$(build-data "$name" "$initial_rarity" "$max_rarity" "$has_unique_equipment" "$piece_type")
    echo
    echo $data
    echo -n "${green}Do you insert data above? (Y/n)$reset > "
    read -k 1 confirm
    echo
    case $confirm in
        [yY]|$'\n')
            insert-character "$data"
            ;;
        *)
            ;;
    esac
}

input-data() {
    local arguments=$@
    echo "$green${bold}Enter the character information$reset"

    [[ -n ${(Q)${(z)arguments[(r)name]}} ]] && {
        echo 'Name (string)'
        echo -n '> '
        read name
    }

    [[ -n ${(Q)${(z)arguments[(r)initial_rarity]}} ]] && while ! validate-number "$initial_rarity"; do
        echo 'Initial Rarity (number, default: 3)'
        echo -n '> '
        read initial_rarity
        [[ -z $initial_rarity ]] && initial_rarity=3
    done

    [[ -n ${(Q)${(z)arguments[(r)max_rarity]}} ]] && while ! validate-number "$max_rarity"; do
        echo 'Max Rarity (number, default: 5)'
        echo -n '> '
        read max_rarity
        [[ -z $max_rarity ]] && max_rarity=5
    done

    [[ -n ${(Q)${(z)arguments[(r)has_unique_equipment]}} ]] && {
        echo 'Unique Equipment (y/N)'
        echo -n '> '
        read -k 1 has_unique_equipment
        echo
        case $has_unique_equipment in
            [yY])
                has_unique_equipment=true
                ;;
            $'\n')
                tput cuu 1
                has_unique_equipment=false
                ;;
            *)
                has_unique_equipment=false
                ;;
        esac
    }

    [[ -n ${(Q)${(z)arguments[(r)piece_type]}} ]] && {
        echo 'Piece Type (string, default: none)'
        echo -n '> '
        read piece_type
        [[ -z $piece_type ]] && piece_type=none
    }
}

build-data() {
    local name=$1
    local initial_rarity=$2
    local max_rarity=$3
    local has_unique_equipment=$4
    local piece_type=$5
    shift 5

    cat << DATA
  {
    name: '$name',
    initialRarity: $initial_rarity,
    maxRarity: $max_rarity,
    hasUniqueEquipment: $has_unique_equipment,
    pieceType: ['$piece_type'],
  },
DATA
}

insert-character() {
    local input=$1
    shift

    sed -i '' '$d' data/character.ts
    echo $input >> data/character.ts
    echo '];' >> data/character.ts
}

validate-number() {
    local input=$1
    shift

    [[ $input =~ [[:digit:]]+ ]]
}

edit-character() {
    local name
    local -A keys=(
        'has_unique_equipment' 'hasUniqueEquipment'
        'piece_type' 'pieceType'
        'max_rarity' 'maxRarity'
    )
    local kind=$1
    local $kind
    local key=${keys[$kind]}
    shift

    echo "$green${bold}Enter the character name to change ${${kind/has_/}/_/ }$reset"

    while true; do
        echo -n 'Name (empty: cancel) > '
        read name
        [[ -z $name ]] && return
        if grep "'$name'" data/character.ts > /dev/null; then
            break
        else
            echo "$name is not found"
        fi
    done

    input-data $kind
    local value=${(P)kind}
    edit-$key "$name" "$value"

    echo "$bold$red$key$reset$bold of $red$name$reset$bold was changed to $value$reset"
}

edit-hasUniqueEquipment() {
    local name=$1
    local value=$2
    shift 2

    edit-data '+normal /hasUniqueEquipment' "+normal Wciw$value"
}

edit-maxRarity() {
    local name=$1
    local value=$2
    shift 2

    edit-data '+normal /maxRarity' "+normal Wciw$value"
}

edit-pieceType() {
    local name=$1
    local value=$2
    shift 2

    edit-data \
        '+normal /pieceType' \
        "+normal f'w" \
        "+if expand('<cword>') == 'none' |
            execute 'normal ciw$value' |
        else |
            execute 'normal f]i, ''$value''' |
        endif"
}

edit-data() {
    vim -es "+normal /'$name'" $@ '+wq' data/character.ts
}

data-diff() {
    local ranges=($(git diff --unified=0 data/character.ts | grep '^@@ .\+ @@' | sed -e 's/^@@ \(.*\) @@.*/\1/'))

    echo --------------------
    local i
    for (( i=1; i <= ${#ranges}; i=i+2 )); do
        local old_range=${ranges[$i]}
        local new_range=${ranges[$i+1]}
        local new_line_count=${${${new_range#*,}/-*/1}/+*/1}

        if [[ $new_line_count -gt 6 ]]; then
            git diff --color =(echo -n) =(tail -n $(( new_line_count + 1 )) data/character.ts | head -n $new_line_count) | format-data-diff
        else
            local new_data=$(extract-data "$new_range" "$(cat data/character.ts)")
            local old_data=$(extract-data "$old_range" "$(git show :data/character.ts)")

            git diff --color --unified=7 =(echo $old_data) =(echo $new_data) | format-data-diff
        fi
        echo --------------------
    done
}

extract-data() {
    local range=$1
    local input=$2
    shift 2

    local line=${${${range%,*}/-}/+}

    local braces_pos=$(head -n $line <<< $input | tail -n 6 | grep -n '{' | awk -F : '{ print $1 }')
    local braces_line_count=$(( line - 6 + braces_pos ))

    sed -n $braces_line_count,$(( braces_line_count + 6 ))p <<< $input
}

format-data-diff() {
    sed 1,5d
}

run() {
    local input=$(read-operation)

    case $input in
        0)
            data-diff
            return 0
            ;;
        1)
            add-character
            ;;
        2)
            edit-character has_unique_equipment
            ;;
        3)
            edit-character piece_type
            ;;
        4)
            edit-character max_rarity
            ;;
        *)
            ;;
    esac

    run
}

run
